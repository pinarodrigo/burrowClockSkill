# start with a launch state
launch
  # greet the user, with their nombre if we have it
  if @name
    say "¿Quieres que localice a @name?."
    -> askForRelocate
  else 
    say "¿A quién quieres que localice?"
  # move on to the askForRelocate state
    -> askForAnother

askForRelocate
  when AMAZON.YesIntent
    -> searchName

  when AMAZON.NoIntent
    # add this question to our next response
    say "¿A quién quieres que localice?"
    # add an automatic re-prompt, in case the user says nothing
    reprompt "Solo dime un nombre"
    -> askForAnother

askForAnother
  when "a $anotherName"
    or "encuentra a $anotherName"
    with $anotherName = AMAZON.FirstName

    @name = $anotherName
    -> searchName

  when AMAZON.YesIntent
      say "<!perfecto.> ¿A quién quieres que localice?"
      -> askForAnother

  when AMAZON.NoIntent
    say "<!chido.>"
    #say "<...1s>"
    -> goodbye
    
  when AMAZON.HelpIntent
    say "<!no hay problema.>"
    say "Solo dime el nombre de la persona que quieres localizar"
    -> askForAnother

searchName
  local speechToText = callLocalizar(@name, context)
  say "<!faltaba más.>"
  say "{speechToText}."
  card "Burrow Clock", map.png, "{speechToText}"
  say "¿Quieres localizar a alguien más?"
  -> askForAnother

  when AMAZON.HelpIntent
    say "<!no hay problema.>"
    say "Solo dime el nombre de la persona que quieres localizar"
    -> askForAnother

  otherwise
    say "¿Quieres que localice a @name?."
    -> askForRelocate

goodbye
  say "<!nos vemos.>"
  END

